buildscript {
  ext.kotlin_version = '1.0.2'
  repositories {
    jcenter()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "com.google.protobuf:protobuf-gradle-plugin:0.7.4"
  }
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.0.0-beta-2'
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:0.14.0' // CURRENT_GRPC_VERSION
    }
  }
  generateProtoTasks {
    all().each { task ->
      task.builtins {
        javanano {
          // Options added to --javanano_out
          option 'ignore_services=true'
        }
      }

      task.plugins {
        grpc {
          // Options added to --grpc_out
          option 'nano'
        }
      }
    }
  }
}

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.2"

  defaultConfig {
    applicationId "me.com.grpc"
    minSdkVersion 16
    targetSdkVersion 23
    versionCode 1
    versionName "1.0"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  lintOptions {
    disable 'InvalidPackage', 'HardcodedText'
    textReport true
    textOutput "stdout"
  }
  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  testCompile 'junit:junit:4.12'
  compile 'com.android.support:appcompat-v7:23.2.1'
  compile 'com.android.support:recyclerview-v7:23.2.1'

  compile 'com.google.code.findbugs:jsr305:3.0.0'
  compile 'com.google.guava:guava:18.0'
  compile 'com.squareup.okhttp:okhttp:2.2.0'
  compile 'io.reactivex:rxjava:1.1.5'
  compile 'io.reactivex:rxandroid:1.2.0'
  compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
  // You need to build grpc-java to obtain these libraries below.
  compile 'io.grpc:grpc-okhttp:0.14.0' // CURRENT_GRPC_VERSION
  compile 'io.grpc:grpc-protobuf-nano:0.14.0' // CURRENT_GRPC_VERSION
  compile 'io.grpc:grpc-stub:0.14.0' // CURRENT_GRPC_VERSION
  compile 'javax.annotation:javax.annotation-api:1.2'
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}